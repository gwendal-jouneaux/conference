ROOT := rooms=ROOMS papers=PAPERS events=EVENTS

ROOMS := '#' _ 'rooms' ENDLINE* rooms=ROOM*
ROOM := _ '-' _ name=STRING _ capacite={',' _ value=INT}? ENDLINE*

PAPERS := '#' _ 'papers' _ ENDLINE* papers=PAPER*
PAPER := '##' _ paperName=STRING ENDLINE* authors=PERSON* abstract=ABSTRACT? video=VIDEO?
PERSON := _ '-' _ name=STRING about={_ ',' _ value=STRING _}? homepage={_ ',' _ value=LINK _}? email={_ ',' _ value=EMAIL _}? ENDLINE*

ABSTRACT := _ '\*abstract\*:' ENDLINE text=MULTISTRING ENDLINE*
VIDEO := _ '\*video\*:' _ url=LINK ENDLINE*

EVENTS := '#' _ 'events' _ ENDLINE* events=EVENT*
EVENT := SimpleEvent | OrganizedEvent | TalkSession
SimpleEvent := '##' _ eventName=STRING ENDLINE* abstract=ABSTRACT? ENDLINE*
OrganizedEvent := '##' _ '\(' _ eventType=STRING _ '\)' _ eventName=STRING ENDLINE* organizers=PERSON* abstract=ABSTRACT? ENDLINE*
TalkSession := '##' _ '\[' _ eventType=STRING _ '\]' _ eventName=STRING ENDLINE* organizers=PERSON* abstract=ABSTRACT? ENDLINE* '###' _ 'papers' _ ENDLINE* papers={ _ '-' _ name=STRING}*

// terminals
EMAIL := '[a-zA-Z0-9.!#$%&\'*+/=?^_`{|}~-]+@[a-zA-Z0-9-]+(?:\.[a-zA-Z0-9-]+)*'
LINK := 'https?:\/\/(www\.)?[-a-zA-Z0-9@:%._\+~#=]{1,256}\.[a-zA-Z0-9()]{1,6}\b([-a-zA-Z0-9()@:%_\+.~#?&//=]*)'
STRING := '[a-zA-Z0-9][a-zA-Z0-9. ]*'
MULTISTRING := '([^\n]+\n)*'
INT := '[0-9]+'
ENDLINE := _ ';|\n'
_ := '[ \t\r\f]*'